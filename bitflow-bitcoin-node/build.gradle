apply from: "${rootDir}/gradle/java.gradle"
apply from: "${rootDir}/gradle/integration-test-set.gradle"
apply plugin: 'org.inferred.processors'
apply plugin: 'com.palantir.java-distribution'
apply plugin: 'com.palantir.docker'

project(':bitflow-bitcoin-node') {

    dependencies {

        // internal dependencies
        compile project(':bitflow-bitcoin-node-api')
        compile project(':bitflow-service-utils')
        compile project (':bitflow-integration-tests')

        // TODO(dwulsin) figure out how to fix this
        //docker project(':bitflow-docker-base')

        // processors
        processor "org.immutables:value:${immutablesVersion}"

        // external dependencies
        compile "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
        compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
        compile "com.fasterxml.jackson.datatype:jackson-datatype-guava:$jacksonVersion"
        compile "com.google.guava:guava:$guavaVersion"
        compile "com.netflix.feign:feign-core:${feignVersion}"
        compile "com.netflix.feign:feign-jackson:${feignVersion}"
        compile "com.netflix.feign:feign-jaxrs:${feignVersion}"
        compile "com.netflix.feign:feign-okhttp:${feignVersion}"
        compile "com.palantir.remoting:http-clients:${httpRemotingVersion}"
        compile "com.palantir.remoting:error-handling:${httpRemotingVersion}"
        compile "io.dropwizard:dropwizard-core:${dropwizardVersion}"
        compile "org.bitcoinj:bitcoinj-core:${bitcoinjVersion}"

        // checks
        compile "com.google.code.findbugs:jsr305:${jsr305Version}"
        compile "com.google.code.findbugs:annotations:${jsr305Version}"

        // unit tests
        testCompile "io.dropwizard:dropwizard-testing:${dropwizardVersion}"
        testCompile "junit:junit:$junitVersion"
        testCompile "org.mockito:mockito-all:$mockitoVersion"
        testCompile "com.github.tomakehurst:wiremock:2.0.6-beta"

        // integration tests
        integrationTestsCompile project(':bitflow-integration-tests')
    }

    def snapshotDockerImage = "docker.io/daedalus2718/bitflow/${project.name}"
    docker {
        name "${snapshotDockerImage}:${project.version}"
        dependsOn distTar
    }

    tasks.docker.doLast {
        exec {
            commandLine "docker", "tag", "--force=true", docker.name, "${snapshotDockerImage}:latest"
        }
    }

    distribution {
        serviceName 'bitflow-bitcoin-node'
        mainClass 'org.drausin.bitflow.bitcoin.BitcoinNodeServer'
        args 'server', 'var/conf/bitflow-bitcoin-node.yml'
    }
}
